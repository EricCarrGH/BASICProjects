' Playfield
DA.DS()=$0613,$050D,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0C05,$050D,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0C05,$1307,
DA.=$0113,$AD02,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$01AD,$AD02,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$01AD,$1302,
DA.=$0113,$AD02,$0406,$0404,$AD07,$0406,$0404,$0404,$0704,$01AD,$AD02,$0406,$0404,$0404,$0704,$06AD,$0404,$0704,$01AD,$1302,
DA.=$0113,$BA02,$0308,$0303,$AD09,$0308,$0303,$0303,$0903,$08AD,$AD09,$0308,$0303,$0303,$0903,$08AD,$0303,$0903,$01BA,$1302,
DA.=$0113,$AD02,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$01AD,$1302,
DA.=$0113,$AD02,$050A,$0505,$AD0B,$0406,$AD07,$050A,$0505,$0C05,$050D,$0505,$0B05,$06AD,$0704,$0AAD,$0505,$0B05,$01AD,$1302,
DA.=$0113,$AD02,$ADAD,$ADAD,$ADAD,$1301,$AD02,$ADAD,$ADAD,$01AD,$AD02,$ADAD,$ADAD,$01AD,$0213,$ADAD,$ADAD,$ADAD,$01AD,$1302,
DA.=$0813,$050F,$0505,$0C05,$AD07,$1301,$0510,$0505,$0B05,$0813,$1309,$050A,$0505,$1105,$0213,$06AD,$050D,$0505,$0E05,$1309,
DA.=$1313,$1313,$1313,$0113,$AD02,$1301,$1302,$1313,$1313,$1313,$1313,$1313,$1313,$0113,$0213,$01AD,$1302,$1313,$1313,$1313,
DA.=$0505,$0505,$0505,$0E05,$AD09,$0308,$1309,$0406,$0404,$1204,$0412,$0404,$0704,$0813,$0903,$08AD,$050F,$0505,$0505,$0505,
DA.=$1313,$1313,$1313,$1313,$AD13,$1313,$1313,$1301,$1313,$1313,$1313,$1313,$0213,$1313,$1313,$13AD,$1313,$1313,$1313,$1313,
DA.=$0505,$0505,$0505,$0C05,$AD07,$0406,$1307,$0308,$0303,$0303,$0303,$0303,$0903,$0613,$0704,$06AD,$050D,$0505,$0505,$0505,
DA.=$1313,$1313,$1313,$0113,$AD02,$1301,$1302,$1313,$1313,$1313,$1313,$1313,$1313,$0113,$0213,$01AD,$1302,$1313,$1313,$1313,
DA.=$0613,$050D,$0505,$0E05,$AD09,$0308,$1309,$050A,$0505,$0C05,$050D,$0505,$0B05,$0813,$0903,$08AD,$050F,$0505,$0C05,$1307,
DA.=$0113,$AD02,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$01AD,$AD02,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$01AD,$1302,
DA.=$0113,$AD02,$050A,$0C05,$AD07,$050A,$0505,$0505,$0B05,$08AD,$AD09,$050A,$0505,$0505,$0B05,$06AD,$050D,$0B05,$01AD,$1302,
DA.=$0113,$BA02,$ADAD,$01AD,$AD02,$ADAD,$ADAD,$ADAD,$ADAD,$13AD,$AD13,$ADAD,$ADAD,$ADAD,$ADAD,$01AD,$AD02,$ADAD,$01BA,$1302,
DA.=$0113,$0510,$0B05,$08AD,$AD09,$0406,$AD07,$050A,$0505,$0C05,$050D,$0505,$0B05,$06AD,$0704,$08AD,$AD09,$050A,$1105,$1302,
DA.=$0113,$AD02,$ADAD,$ADAD,$ADAD,$1301,$AD02,$ADAD,$ADAD,$01AD,$AD02,$ADAD,$ADAD,$01AD,$0213,$ADAD,$ADAD,$ADAD,$01AD,$1302,
DA.=$0113,$AD02,$050A,$0505,$0505,$030E,$050F,$0505,$0B05,$08AD,$AD09,$050A,$0505,$0E05,$0F03,$0505,$0505,$0B05,$01AD,$1302,
DA.=$0113,$AD02,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$ADAD,$01AD,$1302,
DA.=$0813,$050F,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0505,$0E05,$1309

' Standard Atari Sound Note Scale
DA.N()B.=255,243,230,217,204,193,182,173,162,153,144,136,128,121,
DA.B.=114,108,102, 96, 91, 85, 81, 76, 72, 68, 64, 60,
DA.B.=57, 53, 50, 47, 45, 42, 40, 37, 35, 33, 31, 29,
DA.B.=27, 26, 24, 23, 22, 21, 19, 18, 17, 16, 15, 14

DI. AA(880)  byte

DA.font()B.=0,0,0,0,0,0,0,0,
DA.B.=3,3,3,3,3,3,3,3,
DA.B.=48,48,48,48,48,48,48,48,
DA.B.=0,0,0,0,0,255,0,0,
DA.B.=0,0,255,0,0,0,0,0,
DA.B.=0,0,255,0,0,255,0,0,
DA.B.=0,0,0,3,3,3,3,3,
DA.B.=0,0,192,48,48,48,48,48,
DA.B.=3,3,3,3,3,0,0,0,
DA.B.=48,48,48,48,48,192,0,0,
DA.B.=0,0,0,3,3,0,0,0,
DA.B.=0,0,240,12,12,240,0,0,
DA.B.=0,0,255,0,0,252,3,3,
DA.B.=0,0,255,0,0,15,48,48,
DA.B.=3,3,252,0,0,255,0,0,
DA.B.=48,48,15,0,0,255,0,0,
DA.B.=48,48,15,0,0,15,48,48,
DA.B.=3,3,252,0,0,252,3,3,
DA.B.=0,0,213,0,0,0,0,0,
DA.B.=0,0,0,0,0,0,0,0,
DA.B.=0,240,252,223,55,61,15,3,
DA.B.=0,0,0,3,255,127,252,240,
DA.B.=0,0,10,42,40,40,10,0,
DA.B.=12,48,48,40,170,170,170,40,
DA.B.=1,10,10,11,10,2,2,0,
DA.B.=80,104,172,168,184,160,224,128,
DA.B.=5,9,42,42,42,42,10,2,
DA.B.=20,80,122,174,174,170,168,32,
DA.B.=60,195,195,204,60,51,240,0,
DA.B.=60,195,195,51,60,204,12,3,
DA.B.=0,1,1,5,5,5,5,1,
DA.B.=80,212,116,116,84,80,80,64,
DA.B.=0,0,0,204,51,51,204,0,
DA.B.=0,0,48,240,51,51,48,252,
DA.B.=0,0,48,204,15,51,192,252,
DA.B.=0,0,204,204,207,255,12,12,
DA.B.=0,48,204,204,51,207,204,48,
DA.B.=0,0,252,192,243,15,204,48,
DA.B.=0,192,192,204,243,243,204,192,
DA.B.=0,240,204,204,204,240,204,204,
DA.B.=0,240,192,195,243,195,195,243,
DA.B.=0,195,195,51,51,243,51,51,
DA.B.=0,195,51,51,48,48,48,192,
DA.B.=0,51,51,51,195,192,195,195,
DA.B.=0,12,63,63,63,63,12,0,
DA.B.=0,0,0,12,12,0,0,0,
DA.B.=60,255,63,15,63,255,60,0,
DA.B.=48,204,204,204,204,204,204,48,
DA.B.=48,240,48,48,48,48,48,252,
DA.B.=48,204,204,12,48,48,192,252,
DA.B.=48,204,12,48,12,12,204,48,
DA.B.=12,204,204,204,252,12,12,12,
DA.B.=252,192,192,240,12,12,12,240,
DA.B.=48,204,192,240,204,204,204,48,
DA.B.=252,12,12,12,48,48,48,48,
DA.B.=48,204,204,48,204,204,204,48,
DA.B.=48,204,204,204,60,12,204,48,
DA.B.=252,204,204,204,252,192,192,192,
DA.B.=0,0,0,0,0,0,0,0,
DA.B.=0,40,128,128,136,136,136,40,
DA.B.=0,32,32,136,136,168,136,136,
DA.B.=0,136,168,136,136,136,136,136,
DA.B.=0,168,128,128,160,128,128,168,
DA.B.=0,32,136,136,136,136,136,32,
DA.B.=0,136,136,136,136,136,136,32,
DA.B.=0,160,136,136,136,160,136,136



' Enemies         
DA.DE()B.=$38,$7c,$7c,$fe,$fe,$fe,$ae,$ae,$fe,$aa, 'L
DA.B.=$38,$7c,$7c,$fe,$fe,$fe,$ae,$ae,$fe,$54,
DA.B.=$38,$7c,$7c,$fe,$fe,$fe,$ea,$ea,$fe,$aa,  'R
DA.B.=$38,$7c,$7c,$fe,$fe,$fe,$ea,$ea,$fe,$54,
DA.B.=$38,$54,$54,$fe,$fe,$fe,$fe,$fe,$fe,$aa,  'U
DA.B.=$38,$54,$54,$fe,$fe,$fe,$fe,$fe,$fe,$54,
DA.B.=$38,$7c,$7c,$fe,$fe,$fe,$d6,$d6,$fe,$aa,  'D
DA.B.=$38,$7c,$7c,$fe,$fe,$fe,$d6,$d6,$fe,$54,
DA.B.=$38,$7c,$7c,$d6,$fe,$fe,$d6,$aa,$fe,$aa,  'Afraid
DA.B.=$38,$7c,$7c,$d6,$fe,$fe,$d6,$aa,$fe,$54,
DA.B.=0,0,0,$66,$22,$66,0,0,0,0,                'Killed L R U D
DA.B.=0,0,0,$66,$44,$66,0,0,0,0,
DA.B.=0,0,0,$aa,$ee,0,0,0,0,0,
DA.B.=0,0,0,0,$ee,$aa,0,0,0,0,
DA.B.=$40,$a0,$20,$40,$80,$e0,$1c,$17,$1d,$07,  '200
DA.B.=$20,$a0,$a0,$e0,$20,$20,$1c,$17,$1d,$07,   '400
DA.B.=$40,$a0,$40,$a0,$a0,$40,$1c,$17,$1d,$07,   '800
DA.B.=$b8,$a0,$b8,$a8,$b8,$00,$1c,$17,$1d,$07    '1600

' Player
DA.DP()B.=$38,$7c,$fe,$fe,$fe,$fe,$fe,$fe,$7c,$38,  ' Ball
DA.B.=$38,$7c,$fe,$3e,$0e,$0e,$3e,$fe,$7c,$38,    'L
DA.B.=$38,$7c,$3e,$1e,$0e,$0e,$1e,$3e,$7c,$38,
DA.B.=$38,$7c,$fe,$f8,$e0,$e0,$f8,$fe,$7c,$38,    'R
DA.B.=$38,$7c,$f8,$f0,$e0,$e0,$f0,$f8,$7c,$38,
DA.B.=0,$44,$c6,$c6,$ee,$ee,$ee,$fe,$7c,$38,      'U
DA.B.=0,0,$82,$82,$c6,$c6,$ee,$fe,$7c,$38,
DA.B.=$38,$7c,$fe,$ee,$ee,$ee,$c6,$c6,$44,0,      'D
DA.B.=$38,$7c,$fe,$ee,$c6,$c6,$82,$82,0,0,
DA.B.=0,0,0,$82,$c6,$ee,$fe,$7c,$38,0,            'Killed 1
DA.B.=0,0,0,0,0,$c6,$fe,$7c,$28,0,    
DA.B.=0,0,0,0,0,$7c,$ee,$82,0,0,
DA.B.=0,0,0,0,0,$38,$6c,$c6,$82,0,
DA.B.=0,0,0,0,0,$38,$28,$6c,$44,$44,
DA.B.=0,0,0,0,0,$10,$38,$38,$38,$28,
DA.B.=0,0,0,0,0,$10,$10,$10,$10,0,
DA.B.=0,$44,$28,$82,$04,$40,$82,$28,$44,0         'Killed .. 8

DA.C_COLORS()B.=$44,$58,$a8,$38, ' Enemy 1..4
DA.B.=$C6,$44,$84,$28            ' Green, Pellots, Blue/Walls, Yellow/Player

DA.T_READY()B.=167,168,169,170,171
DA.T_GAMEOVER()B.=$BB,$BC,$BD,$BE,0,$BF,$C0,$BE,$C1
DA.T_FRUIT()=$9796, $9998, $9B9A, $9D9C, $9F9E, $9594

DA.L_FRUIT()= 0,1,2,3,4,5,6,7,8,9,10,11,12
DA.L_POW() = 8,6,5,4,3,2,1,0,0,0,0
DA.L_GM()=   5,6,7,10,30000,30000,30000,30000,30000

'  Animation Frames
DA.DAN()=4,0,1,2,1,        ' 0=PL
DA.=0,                     ' 1=
DA.=4,0,3,4,3,             ' 2=PR
DA.=4,0,5,6,5,             ' 3=PU
DA.=10,5,6,9,10,11,12,13,14,15,16, ' 4=Player Killed
DA.=0,                     ' 5=
DA.=2,100,101,             ' 6=EL
DA.=4,0,7,8,7,             ' 7=PD
DA.=2,102,103,             ' 8=ER
DA.=2,104,105,             ' 9=EU
DA.=0,                     ' 10=
DA.=2,108,109,             ' 11=E Avoid
DA.=1,110,                 ' 12=EK L
DA.=2,106,107,             ' 13=ED
DA.=1,111,                 ' 14=EK R
DA.=1,112,                 ' 15=EK U
DA.=0,                     ' 16=
DA.=0,                     ' 17=
DA.=0,                     ' 18=
DA.=1,113                  ' 19= EK D

timer

DIM ANI(19)
a=-1:i=0
repeat
  inc a
  ani(a)=i
  if dan(ani(a))>0 
    for j=i+1 to i+dan(ani(a))
      if dan(j)<100 ' player
        dan(j) = &dp+dan(j)*10
      else ' enemy
        dan(j) = &de+(dan(j)-100)*10
      e.
    n.
    i=j-1
  e.
  inc i
until a=19

' Actor position (ax,ay), sub position (as), direction (ad), intended direction (ai), animation begin:frame:end (ab/af/ae) 
dim ax(4),as(4), ay(4), ad(4), ai(4), ab(4), af(4), ae(4), az(4), li(2),mg(20)
OY=47:OX=59:GC=0:GM=0:powc=186:powt=0:es=0:es_c=0:es_i=0:e_b=0:f_t=0:f_c=0:mge=0
lives=0:mge1=0:mge2=0:mgi=0:quick_start=0:h_c=0:h1=0:h2=0:s_mus_i=0:s_mus_e=0:s_mus_s=0
E_ROAM=0:E_CHASE=1:E_AVOID=2:died=0:si=0:ps=15:e_c=0:score=0:level=0:bits=0
HX=64:HY=56
ad0=&ad:af0=&af:ae0=&ae:ax0=&ax:ay0=&ay

' Playfield / PMG
dim pow(3), pm(4)
GR.12+16:PP=P.(88)+256*P.(89):PPS=PP+83:PPA=&AA+43-PPS:PPC=PP+13*40+20

PMG.1: @ResetPMG
for i=0 to 4:pm(i) = PMADR((i-1)*(i>0)-(i=0))+OY-2:n.

' FONTS
DI.fontbuf(2048)B. 
M=&fontbuf MOD 1024:IF M>0 T. M=M-1024:M=&fontbuf-M
M.&FONT,M,66*8:P.756,M/256
mset &AA,880,0

for y=1 to 20
  j=&ds+40*y+2
  for loc=40*y+2 to 40*y+38
    if p.(j)>18
      c=0  
      if p.(j+1)>18 :inc c: elif p.(j-1)>18 :inc c:e.
      if p.(j-40)>18 :inc c: elif p.(j+40)>18 :inc c:e.
      if c>1 t. aa(loc)=2
    e.
    inc j
  n.
n.
aa(339)=2:aa(339+40*2)=2 ' Enemy home targets
aa(338)=1:aa(341)=1:aa(658)=1:aa(661)=1 ' Spots Enemies cannot switch directions

' Sound
DI.S_MAIN(51)b.,S_EYES(25)b.
for i=0 to 12:
S_MAIN(i*2)=(46-i):s_main(i*2+1)=161
S_MAIN(50-i*2)=(46-i):s_main(50-i*2+1)=161
n.:s_main_e=&s_main+51

for i=0 to 12:
 S_EYES(i*2)=25+i
 S_EYES(i*2+1)=163-1*(I>6)-1*(i>9)
n.:s_eyes_e=&s_eyes+25

DA.S_CHOMP()B.=15,27,19,31,22,35,36,24,33,21,26,14:s_c_i=0:s_c_e=0:s_c_m=&s_chomp+6
DA.S_AVOID()B.=14,1,26,1,14,1,14,2,26,2,26,2,38,2,38,1,0,0,0,0,0,0,0,0::s_avoid_e=&s_avoid+23

for i=0 to 11:S_CHOMP(i)= N(s_chomp(i)):n.
for i=0 to 23-1 step 2:s_avoid(i)=n(s_avoid(i)):s_avoid(i+1)=s_avoid(i+1)+160:n.

@newGame

do
  for frame=1 to 60
  'pause 5
  if died=0
    inc s_mus_i
    p.53760,p.(s_mus_i)
    inc s_mus_i:p.53761,p.(s_mus_i)
  
    if s_c_i<s_c_e
      inc s_c_i: so.1,p.(s_c_i),10,6:so.2,p.(s_c_i),8,4
    e.
  e.

  'pause 10
  ' Inputs
  s = p.(632):bsi=0
  if s<15
    if s<>ps
      if died=0
        ps=s 
        bsi=si
        si=0  
        'if ad(0)<2
        if s&1=0:ai(0)=2: ELIF s&2=0:ai(0)=6:
            'else
        elif s&4=0:ai(0)=-1: ELIF s&8=0:ai(0)=1: e.
        'e.
        
        if ad(0) = 0
          ' Set subposition to check if we can move
          as(0) = 4
        
        elif (ad(0) <> ai(0)) and ( (ad(0)<2 and ai(0)<2) or (ad(0)>1 and ai(0)>1) )
          ' Make immediate 180 degree turns
          ad(0) = ai(0)
          as(0) = 4-as(0) ' Swap the steps to complete
          i=0:@anim ad(0)+1
        e.
      e.
    e.
  e.

  ' Movement Intent
  for i=si to 4
'    if as(i)>10 :@out as(i),20:get k:e.
    if as(i) = 4
      as(i) = 0
      loc = PPS+ay(i)*5+ax(i)/4
      plp = p.(loc+ppa)
      if i=0 
        mm=0
       ' print player position 
        '@out ax(0),10:@out ay(0),20:@out loc+ppa-&aa,30
        ' Check if player interacted with food/elements 
        cp=peek(loc)
        if cp=19
          ' blank space - most likely condition
        elif cp = 173
          ' Normal bit
          POKE loc,19:inc score:@status
          @EatBit
          if s_c_e = s_c_m
            s_c_e = s_c_m+6: else :s_c_e = s_c_m
          e.
          's_c_i=s_c_e-7
        elif cp=172 or cp=186 
          @EatBit
          ' Power bit
          POKE loc,19:score=score+5:@status

          ' Shuffle remaining bits for animation
          for j=0 to powt:if pow(j)=loc
             move &pow+(j+1)*2,&pow+j*2,(powt-j)*2:powt=powt-1:exit
          e.:n.
          ' Update enemy color, state, and direction
          @colorAvoid
          es = E_AVOID:es_c = 100-L_POW(level):e_c=0:gm=2
          @MusicAvoid
          bi = i:for i=1 to 4
            if az(i)<10
              if az(i)=1 t. az(i)=0
              ' Switch dir if not waiting in home at start
              if az(i)<>6
                @switchDir:if as(i)<>0 t. as(i) = 4-as(i) ' Swap the steps to complete
              e.
              @anim 11
            e.
          n.
          i=bi
        elif cp>$93
          ' Ate fruit
          if cp<$A0
            ' Score and show points
            _i = (level+1) * 10:score = score + _i:@out _i*10, PPC-pp+1:frame=0:f_t=99
          e.
        e.
        for j=1 to 4
          if abs(ax(j)-ax(0))<5
            if abs(ay(j)-ay(0))<5
              if es=E_AVOID and az(j)=0
                i=j:@eatEnemy:i=0
              elif az(j)>1
                
              elif not died' and ax(j)=0
                @KillPlayer:exit
              e.
            e.
          e.
        n.
        if died then exit
      elif az(i) = 6

        inc h_c
        @SwitchDir

        'Wait in home and leave steps
        if h_c=1
          as(i)=-5
        elif h_c=h1 or h_c=h2
          as(i)=2
        elif h_c=h1+2 or h_c = h2+1
          ai(i)=1-2*(i=4):ad(i)=ai(i):as(i)=-6
        elif h_c=h1+5 or h_c = h2+2  
          ai(i)=2:ad(i)=2:as(i)=-4
        elif h_c=6 or h_c=h1+8 or h_c = h2+3
          @rotateDir:az(i)=0:as(i)=2
        e.
        if es <> E_AVOID t.  @anim ad(i)+7
          
      else
        mm=1
        if abs(ax(i)-ax(0))<5
          if abs(ay(i)-ay(0))<5
            if es=E_AVOID and az(i)=0
              @eatEnemy
            elif az(i)>1

            elif not died' and ax(j)=0
              @KillPlayer:exit
              'TODO - check why pacman anim goes to Enemy if caught while moving
            e.
          e.
        e.

        ' Enemy Movement AI
        'Normal state
          if az(i)>10
            '@out ax(i),10:@out ay(i),15:@out as(i),20
            'get k
            ' Return home
            'if az(i)=11
            '  az(i)=12
             ' plp=1
            'e.
            if plp>0 
              if ax(i)=hx and ay(i)=hy 
                az(i)=13
                ai(i)=6:ad(i)=ai(i)
                'ax(i)=ax(i)+2:
                pm.i-1,ox+ax(i)+2

              else
                dx=hx-ax(i):dxn=sgn(dx)
                dy=hy-ay(i):dyn=sgn(dy)*2+4

                '@out dx, 8:@out dy, 15:@out dxn, 20:@out dyn, 25:@out ai(i),30
                if abs(dx)< abs(dy) :li(0)=dxn:li(1)=dyn: else :li(1)=dxn:li(0)=dyn: e.
                'li(0)=dyn:li(1)=dxn
                if ad(i)<2 :li(2)=sgn(dy)*-2+4: else  :li(2)=-dxn:e.
                lic=2
                ai(i) = ad(i)

                ' Choose direction
                for _i=0 to lic
                  if li(_i) <> 0 and li(_i)<>4 and (az(i)=11 or li(_i) = ad(i) or (ad(i)<2 and abs(li(_i)) <> abs(ad(i)))  or (ad(i)>1 and abs(li(_i)-4) <> abs(ad(i)-4)) )
                    if li(_i)<2 : l2 = loc + li(_i) : else : l2 = loc + 20*(li(_i)-4) : e.
                    if peek(l2)>18
                      if li(_i)<>ad(i)
                        ai(i)=li(_i):ad(i)=ai(i)
                        if az(i)>7 or es < E_AVOID t. @anim ad(i)+1+(i>0)*6
                      e.
                      exit
                    e.
                  e.
                n.
                az(i)=12
              e.
            e.
          elif plp>1
            if az(i)<2
              if ad(i)<2
                'if ai(i)=2:ai(i)=6:else:ai(i)=2:e.
                if rand(2)=1:ai(i)=6:else:ai(i)=2:e.
              else
                'if ai(i)=1:ai(i)=-1:else:ai(i)=1:e.
                if rand(2)=1:ai(i)=-1:else:ai(i)=1:e.
              e.
            e.
          e.  
        e.    
      

      ' Switch directions if requested
      if plp>mm
        if ai(i)<>ad(i) 
          if ai(i)<2 : l2 = loc + ai(i) : else : l2 = loc + 20*(ai(i)-4) : e.
          if peek(l2)>18 
            ad(i)=ai(i)
            if i=0 or az(i)>0 or es < E_AVOID t. @anim ad(i)+1+(i>0)*6
          elif i=0 
            if bsi=1 t. si=1
          e.
        e.
      e.

      ' Check ability to move
      if ad(i)<2 : l2 = loc + ad(i) : else : l2 = loc + 20*(ad(i)-4) : e.
      cp = peek(l2)
      if cp<19  ' Hit wall
        ' Alive actor
        if az(i)<5
          ' Don't block if wrapping around
          nx=ax(i)+ad(i)
          if nx>-8 and nx<141
              ' Hit wall
              if i>0 ' Enemy must change direction
                
                if az(i)=2 ' Inside home, revert to normal look/color
                
                  @switchDir
                  az(i)=1* (es=E_AVOID):poke 703+i,C_COLORS(i-1)
                  ' Remove from movement groups
                  mv=0:for _i=4 to mge1-1:if mg(_i)=i t. mv=1:if mv=1 :mg(_i)=mg(_i+1):e.:n.
                  mv=0:for _i=10 to mge2-2:if mg(_i)=i t. mv=1:if mv=1 :mg(_i)=mg(_i+2):e.:n.
                  mge1=mge1-1:mge2=mge2-2:mgi=mgi-1
                  if mgi=0 
                    if es=e_avoid :@MusicAvoid: else :@MusicNormal: e.
                  e.
                else
                  @rotateDir
                e. 

                if es < E_AVOID or az(i)>0 t. @anim ai(i)+7
              else ' Stop player
                ai(0)=0: si=1
                af(0)=ab(0)+1
                MOVE DAN(af(0)),pm(0)+2+ay(0),10
              e.
              ad(i)=ai(i)
          else
            ' Wrap around
            if nx<-12 : ax(i) = 144 : elif nx>144 :ax(i)=-12: e.
          e.
        elif az(i)=13
          az(i)=2
        else
        e.
      e.
    e.

  n.
  if died=0
  inc s_mus_i:if s_mus_i>s_mus_e t. s_mus_i = s_mus_s  
  p.53760,p.(s_mus_i)
  inc s_mus_i:p.53761,p.(s_mus_i)
  if s_c_i<s_c_e
    inc s_c_i: if s_c_i < s_c_e :so.1,p.(s_c_i),10,6:so.2,p.(s_c_i),8,4
    else :so.1,0,0,0:so.2,0,0,0:e.
  e.
  e.
  

'Animation
inc subf2
if subf2=10
  subf2=0
  if key():@Mute:get k:repeat:until key():get K:@UnMute:e.

  ' Enemy
  for i=1 to 4
    inc af(i)
    if af(i)>ae(i) t. af(i) = ab(i)
    MOVE DAN(af(i)),pm(i)+2+ay(i),10
  n.

  ' End of avoid state blinking
  if es=E_AVOID: if es_c>97
    for _i=1 to 4:if az(_i)=0 t. poke 703+_i,E_B:n.
    'MSET 704,4, e_B
  e.:e.

  ' Power bits animation
  if powc = 172 :powc=186:e_b=$84: else :powc=172:e_b=$0c: e.
  for i=0 to powt:poke pow(i),powc:n.
e.

' Movement and rendering

if si=0
  ' player animation
  inc af(0)
  if af(0)>ae(0) t. af(0) = ab(0)
  MOVE DAN(af(0)),pm(0)+2+ay(0),10
  
  if dpeek(ad0)<2
    ' Horizontal movement
        x = d.(ax0)+d.(ad0)
        inc as(0):dp.ax0,x:x=x+ox
        pause
        PM.7,X:PM.6,X+2:PM.5,X+4:PM.4,X+6
  else
    ' Vertical movement 
    inc as(0):dy = d.(ad0)-4:d.ay0,d.(ay0)+dy:loc = PM(0)+d.(AY0)
    pause
    if dy<0 : move loc-dy,loc,14: else :-move loc-dy,loc,14:e.
 e.
else 
pause
e.

inc gc:if gc<gm 
  mgs=0:mge=mge1
else 
  gc=0:mgs=10:mge=mge2
e.
aaa=0
for j=mgs to mge
  i=mg(j)
  if ad(i)<2
    ' Horizontal movement
    inc as(i):ax(i) = ax(i) + ad(i):pm.i-1,ox+ax(i)
  else
    ' Vertical movement 
    inc as(i):dy = ad(i)-4:ay(i)=ay(i)+dy:loc = PM(i)+AY(I)
    if dy<0 : move loc-dy,loc,14: else :-move loc-dy,loc,14:e.
  e.
  'if az(i)>10 :inc aaa: @out aaa,10: @out as(i),30:get k:e.
n. 

n.

' End of 60 frames - increase enemy state counter and do other checks
  if f_t<100
    inc f_t
    if f_t=100 t. @ClearMsg
  e.

  inc es_c
  if es_c>99
    
    ' Check for death
    if died=1 
      i=0:@anim 4
      for i=0 to 3:MSET PMADR(I),256,0:n.
      MOVE DAN(af(0)),pm(0)+2+ay(0),10
      pause 30
      poke 53761,164
      for _j=42 to 52 step 2
      inc af(0):MOVE DAN(af(0)),pm(0)+2+ay(0),10
        for _i = _j to _j+7
          poke 53760, _i:for _k=0 to 40:n.
        n.
        for _i = _j+7 to _j step -1
          poke 53760, _i:for _k=0 to 40:n.
        n.
          if _j mod 3 = 0 :
           inc af(0):MOVE DAN(af(0)),pm(0)+2+ay(0),10
          e.
        for _i = _j-1 to _j-15 step -1
          poke 53760, _i:for _k=0 to 20:n.
        n.
      n.
    
      inc af(0):MOVE DAN(af(0)),pm(0)+2+ay(0),10
      for _j=0 to 1:for _i=100 to 40 step -1:
           poke 53760, _i:for _k=0 to 20:n.
      n.:@mute:pause 5:poke 53761,164:n.

        @mute
      'repeat 
       ' pause 6
        'inc af(0):MOVE DAN(af(0)),pm(0)+2+ay(0),10
        
      'until af(0)>ae(0)
      pause 30
      @ResetPMG
      pause 60
      if lives>0 t. @fadeOut 2
      @restartLevel
    else
      es_c=0
      ' End of state 
      
      ' If was avoid, set enemies to normal color/anim
      if es=E_AVOID
        @MusicNormal
        for i=1 to 4:@anim ad(i)+7:n.: @colorNormal:gm=l_gm(level):
      e.
      inc es_i
      es=E_ROAM
    e.
  e.

  framet=time-framet
  framet = int((60.0/framet)*60)
  mset pp,5,0:@out framet,2':@out fre(),10
  framet=time
  

loop

PR.Mute:mset 53760,8,0:ENDP.
PR.UnMute:p.53761,161:endp.
PR.EatBit
  ' Track bits to show fruit and trigger end of level
  inc bits 
  if bits=70 or bits=170
    @msg &t_fruit+l_fruit(level)*2,2
    f_t = 95
  elif bits>259 ' Ate all - beat the level!
    sound ' clear all sound
    af(0)=ab(0)
    MOVE DAN(af(0)),pm(0)+2+ay(0),10
    pause 45
    j=$0e:for i=0 to 7:poke 710,j:pause 20:if j=$0e :j=$84:el.:j=$0e:e.:n.
    pause 45
    @FadeOut 1
    @nextLevel
  e.
ENDP.

PR.KillPlayer
  @mute
  died=1:es_c=99:frame=20:si=1:es=E_ROAM:mge1=0:gm=255:mge1=-1
  for _i=0 to 4:ai(_i)=0:ad(_i)=0:as(_i)=0:az(_i)=0:n.
ENDP.

PR.eatEnemy

  az(i)=11 ' dead

  ' Calculate points earned
  inc e_c:_j = 20
  for _i = 2 to e_c:_j = _j * 2 : n.
  score = score + _j:@status

  'Show enemy as points, hide player, wait
  MOVE &DE+130+10*e_c,pm(i)+2+ay(i),10
  poke 703+i,$b8
  MSET pm(0)+ay(0)+2,10,0

  ' Play sound
  for _i=100 to 20 step -2
  poke 53760,_i:poke 53761,164
  for _j=0 to 50:n.
  if _i>40 t. _i=_i-1
  poke 53761,0 :for _j=0 to 40:n.
  n.
  pause:poke 53761,0
  @MusicEyes

  ' Snap enemy to grid to enable fast movement
  if as(i)>0 
    MSET pm(i)+ay(i)+2,10,0
    if ad(i)<2: ax(i)=ax(i)-ad(i)*as(i):
    else :ay(i)=ay(i)-(ad(i)-4)*as(i):e.
    as(i)=0
  e.

  ' Show player
  MOVE DAN(af(0)),pm(0)+2+ay(0),10
  
  ' Change enemy to eyes
  @anim ad(i)+7
  poke 703+i,$0e

  ' Make enemy eyes move faster
  mg(4+mgi)=i
  mg(10+mgi*2)=i
  mg(10+mgi*2+1)=i
  inc mgi
  inc mge1
  mge2=mge2+2
  
ENDP.

PR.switchDir
  if ad(i)<2
    ad(i) = -ad(i):
  else
    ad(i) = 8-ad(i)
  e.
  ai(i) = ad(i)  
ENDP.

PR.rotateDir
  ' If the enemy already has an adjacent intent, we only come here if it is
  ' blocked, so just go the opposite direction.
  ' If still heading straight, pick a random direction for now
  if ad(i)>1
    if ai(i)<2 
        ai(i) = -ai(i)
      else
        if rand(2)=1:ai(i)=-1:else:ai(i)=1:e.  
    e.
  else
    if ai(i)>1
      ai(i) = 8-ai(i)
    else
      if rand(2)=1:ai(i)=6:else:ai(i)=2:e.
      e.
  e.
  ad(i) = ai(i)  
ENDP.

PR.anim offset
  if az(i)>10:if i>0:if offset=11 then exit:offset=offset+6:e.:e.
  ii = ani(offset)
  ab(i) = ii+1
  ae(i) = ii+dan(ii)
  af(i) = ab(i)
  MOVE DAN(af(i)),pm(i)+2+ay(i),10
ENDP.

PR.FadeIn
for _i=0 to 7:p.704+_i,c_colors(_i)- (c_colors(_i) mod 16) :n.
for _j=2 to 14 step 2
  pause:for _i=704 to 711:c=p.(_i): if (c mod 16)< c_colors(_i-704) mod 16 t. p._i,c+2:n.
n.
ENDP. 

PR.FadeOut stt
for _j=0 to 14 step stt
  pause:for _i=704 to 711:c=p.(_i): if (c mod 16)>0 :p._i,c-1: el. :p._i,0:e.:n.
n.
ENDP.

PR.colorNormal
move &c_colors,704,8
for _i=1 to 4:if az(_i)=1 t. az(_i)=0:next i
@ColorEaten
ENDP.

PR.colorAvoid
MSET 704,4, $84
@ColorEaten
ENDP.

PR.colorEaten
for _i=1 to 4:if az(_i)>10 t. poke 703+_i,$0e:n.
ENDP.

PR.newGame
  level=-1:score=0:lives=3:f_c=0:noLifeLost=0:ISI=0
  @nextLevel
ENDP.

PR.nextLevel
  mset 704,8,0
  ' Load screen map
  mset pp,40*24,0
  MOVE &DS,PP+40,40*22
  inc level
  bits=0:powt=3
  pow(0)= pp+40*4+3:pow(1)= pp+40*4+36
  pow(2)= pp+40*17+3:pow(3)= pp+40*17+36
  
  'Draw fruit for this level in lower right
  for i=0 to level:move &t_fruit+l_fruit(i)*2,pp+956-(i*2),2:n.
  noLifeLost=level>0
  @restartLevel
ENDP.

PR.restartLevel 

' Check for game over
if noLifeLost=0 and lives = 0
  for i=0 to powt:poke pow(i),0:n.
  @MSG &T_GAMEOVER,9
  repeat:until key():get K
  @newGame
  exit
e.
@ResetPMG
' Set Colors

@MSG &T_READY,5
@showlives

es=E_ROAM:es_c=0:es_i=0:died=0:frame=0:f_t=0
si = 0 ' start at 0 (if player is moving) else 1 (just the Enemys)
ps = 15

gm=l_gm(level):gc=0

' Movement groups
for i=0 to 3:mg(i)=i+1:n.
mgs=0:mge1=3:mge2=9:mgi=0

poke pp+37,175

ax(0)=66:ay(0)=120:ai(0)=-1:ad(0)=-1:as(0)=0

'ax(1)=68:ay(1)=56:ai(1)=-1
'ax(2)=0:ay(2)=0:ai(2)=6
'ax(3)=132:ay(3)=0:ai(3)=-1
'ax(4)=0:ay(4)=152:ai(4)=2
ax(1)=66:ay(1)=56:ai(1)=-1:ad(1)=-1:az(1)=0:as(1)=2
ax(2)=66:ay(2)=72:ai(2)=6:ad(2)=6:az(2)=6:as(2)=3
ax(3)=56:ay(3)=72:ai(3)=2:ad(3)=2:az(3)=6:as(3)=2
ax(4)=76:ay(4)=72:ai(4)=2:ad(4)=2:az(4)=6:as(4)=2

' Set duration in which enemies exit their home
h_c=0
if level=0
  h1=33:h2=63
elif level=1
  h1=7:h2=39
else
  h1=7:h2=16
e.


@DEBUG_VARS
@FadeIn
@introsong
for i=4 to 0 step -1
  
  if i=0  
    as(0)=2
  '  if not quick_start t. pause 60  
    if noLifeLost=0
      lives = lives - 1
      @showlives
    e.
    noLifeLost=0
  e.

  @anim ai(i)+1+(i>0)*6
  if i > 0: pm.i-1,ox+ax(i): else : x=ox+ax(i):PM.4,X+6:PM.5,X+4:PM.6,X+2:PM.7,X : e.
n.
@introsong
'for i=&aa to &aa+879:if p.(i)>0 then poke i-PPA,43+p.(i):n.

pause 5

i=2:@anim 2+1+6

@ClearMsg

@MusicNormal
s_mus_i = s_mus_i+2
framet=time
subframe=0
s_c_i=s_c_m+99:s_c_e=0

poke 53761,161 ' Main background volume 1

ENDP.


PR.MusicNormal
s_mus_s=&s_main:s_mus_i=s_mus_s-1:s_mus_e=s_main_e
ENDP.

PR.MusicAvoid
s_mus_s=&s_avoid:s_mus_i=s_mus_s-1:s_mus_e=s_avoid_e
ENDP.

PR.MusicEyes
s_mus_s=&s_eyes:s_mus_i=s_mus_s-1:s_mus_e=s_eyes_e
ENDP.


PR.ShowLives
add=PPS+21*40 
MSET add,10,0
for j=1 to lives:P.add,174:add=add+2:n.
ENDP.

PR.ClearMsg
MSET PPC-6,12,19
ENDP.

PR.MSG add len
MOVE add,PPC-(len/2),len
ENDP.

PR.status
 sloc=pp+36:s=score
 repeat
  r = s mod 10:poke sloc,175+r:sloc = sloc - 1:s = (s - r)/10
 until s<1
ENDP.

PR.out s offset
 _s = sgn(s)
 s=abs(s)
 sloc=pp+offset
 repeat
  r = s mod 10
  poke sloc,175+r
  sloc = sloc - 1
  s = (s - r)/10
 until s<1
 poke sloc,19+ 112*(_s<0)
ENDP.

PR.ResetPMG
P.623,1+16 ' All players in front + 5th player
FOR I=0 TO 4:MSET PMADR(I*(i<4)-(i=4)),256,0:P.53255+I,0:N.
ENDP.

PR.IntroSong
DA.SH()B.=25,0,37,0,32,0,29,0, 37,32,0,0,29,29,0,0,
DA.    B.=26,0,38,0,33,0,30,0, 38,33,0,0,30,30,0,0,
DA.    B.=25,0,37,0,32,0,29,0, 37,32,0,0,29,29,0,0,
DA.    B.=28,29,30,0,30,31,32,0, 32,33,34,0,37,37,37,37
DA.SL()B.=1,0,0,0,0,0,8,0, 1,0,0,0,0,0,8,0,
DA.B.=2,0,0,0,0,0,9,0, 2,0,0,0,0,0,9,0,
DA.B.=1,0,0,0,0,0,8,0, 1,0,0,0,0,0,8,0,
DA.B.=8,0,0,0,10,0,0,0, 12,0,0,0, 13,0,0,0
if quick_start=1 then exit
if ISI >60 : pause 20:exit:e.
NL=0:NH=0:hvol=8
for i = ISI  to ISI+31
if I>ISI t. pause 3
if SL(i)>0 :NL=N(SL(i)):vol=6:vol2=3: elif vol>0 :vol=vol-2:vol2=0: e.:so. 0,NL,10,vol:so. 3,NL,8,vol2
IF SH(i)>0 :NH=N(SH(i)):hvol=6: else: hvol=0: e.:so. 1,NH,10,hvol
n.
ISI=i
pause
if ISI>60 
for i= 6 to 0 step -2:so. 1,NH,14,I:pause:n.:
for i=0 to 2:so. i,0,0,0:n.
e.
ENDP.



' ===================================================================================
' Uncomment to set for temporary conditions
PR.DEBUG_VARS
'gm=2
'es=E_AVOID
'@colorAvoid
'ax(4)=0+5*8:ay(4)=152-4*8:ai(4)=1
'quick_start=1

'ax(0)=62:ay(0)=56:ai(0)=1:ad(0)=1 ' Player hit ghost
'i=1:@eatEnemy
endp.

'AZ states:
'0 : normal - normal  (normal
'1 : normal - exited home - don't blink
'2 : exiting home
'6 : in home - game start - waiting 
'8+: dead
'11: dead - eyes - change dirs
'12: dead - eyes
'13: dead - entering home
  
